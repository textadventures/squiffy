---
title: Basic concepts
sidebar:
    order: 1
---

import Sample from "@components/Sample.astro";
import {Aside} from "@astrojs/starlight/components";

You can write Hello World with no markup at all:

<Sample script="Hello, World!

"></Sample>
    
You can format text using [Markdown](http://daringfireball.net/projects/markdown/syntax), and also use HTML.

<Sample script="Here is some **bold text** and some _italic text_.

    A list:
    - Item 1
    - Item 2
    - Item 3

    A link to [Google](https://www.google.com).

    Using a `&lt;span&gt;` tag to &lt;span style=&quot;color: red&quot;&gt;change the colour&lt;/span&gt; of some text.
"></Sample>

To create interactive stories, you'll need to add some links. There are two types of text block you can use in Squiffy:

- **Sections** are the main units of text.
- **Passages** are smaller units which exist within sections.

Taking the player to a new section will deactivate all previous links, so use a new section when the player has taken some action to move the story forward.

Within a section, you can link to passages. After the player clicks a link to a passage, links to other passages in the same section will remain active.

## Sections

Section names must be unique within a story. Set up a section using double square brackets, followed by a colon. To link to a section, use double square brackets.

<Sample script="Do you want to have the [[roast chicken]] or skip straight to [[dessert]]?
    
[[roast chicken]]:
This plate of roast chicken looks delicious.

[[dessert]]:
Three different flavours of ice cream - yum!"></Sample>

To use different link text, put the name of the section in brackets afterwards.

```
This is [[how to use custom link text]](section2).
```

## Passages

Set up a passage using single square brackets, followed by a colon. Passages can link to further passages. After the player clicks a link to a passage, that link is deactivated, so the player can only click it once. To link to a passage, use single square brackets.

<Sample script="Looking around the room, you can see a [TV], a [book] and a piece of [paper] with some handwriting scribbled on it.

[TV]:
Covered in dust and the plug is missing.

[book]:
It's a book of magic tricks. Maybe you should [open] it?

[open]:
The cover opens up to reveal the pages all glued together, with an empty key-shaped hole cut out.

[paper]:
&quot;Gone out for a walk. May be a while.&quot;"></Sample>

Passage links can be explored in any order - in the example above, the player might look at the book first, then look at the TV, then open the book. Only after clicking a link to another section will any remaining passage links be deactivated.

Passage names must be unique within their section.

To use different link text, put the name of the passage in brackets afterwards.

```
This is [how to use custom link text](passage1).
```

## Continue links

Where you have a section with one link that simply goes to the following section, you can automatically create a "Continue" link like this:

<Sample script="This is the first part of my story.

            +++Continue...

            This is the second part.

            +++I want to hear more...

            Very well. Here is the third part."></Sample>

## JavaScript

Any section or passage can call some JavaScript when it is displayed. Simply indent with four spaces or a tab, before the text.

<Sample script="Clicking this [link] will show an alert.

    [link]:
    &#32;&#32;&#32;&#32;alert (&quot;Hello!&quot;);

    Text for the passage."></Sample>

## Turn counting

You can trigger a passage after the player has made a certain number of clicks within a section. For example, you can display some extra text, to indicate the passage of time. Or, the passage might run some JavaScript to automatically move the player to another section.

In the example below, the text "We're nearly here. The train is pulling into the platform." is always written after the first passage click. After the second passage click, we are moved into the next section.

<Sample script="On the train you can see a [girl singing], a [man reading a book] and an [old woman].

    [girl singing]:
    She is nodding her head to the music in her enormous earphones, and singing badly out of tune.

    [man reading a book]:
    He's been reading the same page of *War and Peace* for a while now.

    [old woman]:
    She eyes you suspiciously, as if she has seen your type before.

    [@1]:
    We're nearly here. The train is pulling into the platform.

    [@2]:
    &#32;&#32;&#32;&#32;squiffy.story.go(&quot;station&quot;);

    [[station]]:
    We have now arrived at the station."></Sample>

You can also trigger a passage to be written only after all the passages in that section have been clicked on.

See the example below where after all passage links have been clicked the text "You have examined every item." will display.

<Sample script="Looking at the detective's desk you can see the following items to examine; a [photograph], a [file] and a [coffee mug].

    [photograph]:
    Looks like some kind of work celebration. The detective is in the middle of the picture holding a basket of fruit and other officers are toasting him.

    [file]:
    The file is empty. You notice though that the tab has your brother's name on it!

    [coffee mug]:
    It is just a coffee mug.

    [@last]:
    You have examined every item."></Sample>

## Clear the screen

Any section or passage can clear the screen using @clear on a line on its own:

```
[[Chapter 2]]:
@clear
My first reaction to the explosion was...
```

## Set attributes

<Aside type="caution">TODO: Create a separate page for Attributes</Aside>

<Aside type="caution">TODO: Need a full interactive sample here (e.g. choosing from a food menu)</Aside>

You can set an attribute within a section or passage like this:

```
@set score = 1000
```

If the value is a number, it will be stored as a number. Otherwise, it will be stored as a string.

For boolean (true/false) values, to set as true:

```
@set my_true_value
```

and to set as false:

```
@set not my_false_value
```

or alternatively:

```
@unset my_false_value
```

For number values, you can increase or decrease the value by 1:

```
@inc score
@dec health
```

Or to increase or decrease by other amounts:

```
@inc score 100
@dec health 5
```

You can also set an attribute value from a link:

```
Are you [[male]](start, gender=male) or [[female]](start, gender=female)?

[[start]]:
Your choice has been recorded.
```

And you can also set a value in JavaScript:

```
    squiffy.set("gender", "female");
```

## Read attributes

You can display the value of an attribute using `{{name}}` (if the attribute name is all one word), or `{{get "attribute name"}}`.

```
You chose {{gender}}.
```

You can also read a value using JavaScript:

```
    var gender = squiffy.get("gender");
```

You can conditionally display text depending on the value of a boolean attribute using `{{#if (get "attribute name")}}`.

<Aside type="caution">TODO: Comparing string attributes. The following examples are out of date.</Aside>

```
{if gender=male:You are a man.}{else:You are a woman.}
```

To compare two attributes, add an `@` before the second attribute name:

```
{if a=@b:attribute value a and attribute value b are equal.}{else:They are different.}
```

## Embed text

You can embed text from another section, or from a passage in the current section, using `{{embed "section or passage name"}}`.

<Sample script="[[section1]]:
Here is some text from the next section: {{embed &quot;section2&quot;}}

    Here is some text from a passage in this section: {{embed &quot;passage&quot;}}

    [passage]:
    Text from passage.

    [[section2]]:
    Text from next section."></Sample>

## Track which sections and passages have been seen

You can tell if the player has seen a passage or section using JavaScript:

```
    if (squiffy.story.seen("passage3")) alert ("You have seen passage3!");
```

You can also conditionally display text. Here we're using `{{#if}}` with the
`seen` helper to check if the passage "open" has been seen.
Try clicking the "cupboard" link first, to see the description of the closed
cupboard. Then, Restart the game, and now click "open" first, then "cupboard",
to see the difference.

<Sample script="You can see a [cupboard]. Maybe you should [open] it?

    [open]:
    You open the cupboard.

    [cupboard]:
    The cupboard is
    {{#if (seen &quot;open&quot;)}}
    &#32;&#32;&#32;&#32;open, and there are empty bottles inside.
    {{else}}
    &#32;&#32;&#32;&#32;closed.
    {{/if}}"></Sample>

## Set the start section

<Aside type="caution">TODO: Note that changing or removing the `@start` in the sample below
    doesn't currently trigger the runtime to update the game start.</Aside>

By default, the story begins in the first section. You can choose a different section like this:

<Sample script="@start Beginning

    [[some other section]]:
    This section would normally be the start, but we have overridden it.

    [[Beginning]]:
    This is where the story begins."></Sample>

## Set the title

Set the title like this:

```
@title My Amazing Interactive Story
```

## Master sections and passages

An empty name for a section or passage creates a "master", which is triggered for every section or passage in the game. (A master passage defined in a named section will only be triggered for every passage in that section)

e.g. to clear the screen before every section, and to increase a global turn counter:

```
[[]]:
@clear
@inc turns
    if (squiffy.get("turns") > 5) ...
```

## Importing files

*Importing files only works in the command-line version of Squiffy*

You can split your script up into multiple files and import them:

```
@import other_file.squiffy
```

You can also use the same syntax to import external JavaScript files:

```
@import my_script.js
```

This syntax also accepts wildcards, so you can include all .squiffy files in a directory:

```
@import *.squiffy
```

## Custom HTML and CSS

The `index.template.html` and `story.template.css` files are customisable. It is better not to edit the ones in your Squiffy directory - you can copy them to the same directory as your .squiffy story script file, and make edits there instead.